#version 460
#extension GL_GOOGLE_include_directive : enable

#include "layout.glsl"
#include "../raytrace.glsl"
#include "restir_di.glsl"

#include "merian-shaders/hash.glsl"
#include "merian-shaders/random.glsl"
#include "merian-shaders/bsdf_diffuse.glsl"
#include "merian-shaders/bsdf_ggx.glsl"
#include "merian-shaders/normal_encode.glsl"
#include "merian-shaders/color/colors_yuv.glsl"

#define pixel ivec2(gl_GlobalInvocationID)
#define resolution ivec2(imageSize(img_irradiance))
#define first_hit hits[image_to_buffer_index(pixel, resolution)]
#define read_reservoir(pixel) reservoirs[image_to_buffer_index((pixel), resolution)]
#define write_reservoir reservoirs[image_to_buffer_index((pixel), resolution)]

void main() {
    if (any(greaterThanEqual(pixel, resolution))) {
        return;
    }

    uint rng_state = pcg4d16(uvec4(pixel, params.frame * 4 + 3, SEED));

    ReSTIRDIReservoir reservoir = read_reservoir(pixel);

    vec3 irr = vec3(0);

    if (restir_di_light_sample_valid(reservoir.y)) {
        Hit next_hit;
        const vec3 wo = normalize(reservoir.y.pos - first_hit.pos);
        const float wodotn = dot(wo, first_hit.normal);
        next_hit.wi = wo;
        next_hit.pos = first_hit.pos - first_hit.wi * 1e-3;
        f16vec3 incident = f16vec3(0); // contains throughput already
        f16vec3 throughput = f16vec3(1);
        trace_ray(throughput, incident, next_hit);

        if (VISIBILITY_SHADE && abs(distance(first_hit.pos, reservoir.y.pos) - distance(first_hit.pos, next_hit.pos))
                            / max(distance(first_hit.pos, reservoir.y.pos), distance(first_hit.pos, next_hit.pos)) > 0.1) {
            // not visible 
            restir_di_reservoir_discard(reservoir);
            write_reservoir = reservoir;
        } /*else if (APPLY_MV == 1) {
            // update mv
            reservoir.y.mv = (next_hit.pos - next_hit.prev_pos) / TIME_DIFF;
            write_reservoir = reservoir;
        }*/

        const float microfacet_bsdf = bsdf_ggx_diffuse_mix_times_wodotn(first_hit.wi, wo, first_hit.normal, bsdf_ggx_roughness_to_alpha(first_hit.roughness), 0.02);
        if (!isnan(reservoir.w_sum_or_W) && !isinf(reservoir.w_sum_or_W)) {
                irr = microfacet_bsdf * reservoir.y.radiance * reservoir.w_sum_or_W * max(dot(reservoir.y.normal, -wo), 0) / pow(distance(reservoir.y.pos, first_hit.pos), 2);
        }
    }    

    imageStore(img_irradiance, pixel, vec4(irr, 1));
    const float l = yuv_luminance(irr);
    imageStore(img_moments, pixel, vec4(l, l * l, 0, 0));
}
